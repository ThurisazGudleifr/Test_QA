
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("РежимВыбора") Тогда
	
		Элементы.СписокМетаданных.РежимВыбора = Параметры.РежимВыбора;
		Элементы.ОтборМетаданных.РежимВыбора = Параметры.РежимВыбора;
		
		ЗакрыватьПриВыборе = Истина;
		
	КонецЕсли;
	
	ЗаполнитьДеревоОтборМетаданных();
	ЗаполнитьСписокВыбора();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтбор(Неопределено);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы


#Область ОбработчикиСобытийТабличнойЧастиОтборМетаданных

&НаКлиенте
Процедура ОтборМетаданныхПриАктивизацииСтроки(Элемент)
			
	ПодключитьОбработчикОжидания("УстановитьОтборСписокМетаданных", 0.1, Истина); 
		
КонецПроцедуры

&НаКлиенте
Процедура ОтборМетаданныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
		
	УстановитьОтборСписокМетаданных();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийТабличнойЧастиОтборМетаданных


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьЭлемент(Команда)
	
	ТекущиеДанные = Элементы.СписокМетаданных.ТекущиеДанные;
	
	Структура = Новый Структура;
	Структура.Вставить("ОбъектМетаданных", ТекущиеДанные.ОбъектМетаданных);
	Структура.Вставить("КлассМетаданных", ТекущиеДанные.КлассМетаданных);
	
	ОповеститьОВыборе(Структура);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы


#Область ОбработчикиСобытийТабличнойЧастиСписокМетаданных

&НаКлиенте
Процедура СписокМетаданныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьЭлемент(Неопределено);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийТабличнойЧастиСписокМетаданных


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокВыбора()
		
	Для Каждого ОбъектМетаданных Из Метаданные.Справочники Цикл
		
		НоваяСтрока = СписокМетаданных.Добавить();
		НоваяСтрока.ОбъектМетаданных = ОбъектМетаданных.Имя;
		НоваяСтрока.КлассМетаданных = "Справочник";
		
	КонецЦикла;	
			
	Для Каждого ОбъектМетаданных Из Метаданные.ПланыВидовХарактеристик Цикл
		
		НоваяСтрока = СписокМетаданных.Добавить();
		НоваяСтрока.ОбъектМетаданных = ОбъектМетаданных.Имя;
		НоваяСтрока.КлассМетаданных = "ПланВидовХарактеристик";
		
	КонецЦикла;		
		
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьОтборСписокМетаданных()
	
	ОтборТекущиеДанные = Элементы.ОтборМетаданных.ТекущиеДанные;
	
	Если ОтборТекущиеДанные.Значение <> "Метаданные" Тогда
	
		ФиксированнаяСтруктура = Новый ФиксированнаяСтруктура("КлассМетаданных", ОтборТекущиеДанные.Значение);
		
	Иначе
		
		ФиксированнаяСтруктура = Неопределено;
		
	КонецЕсли;
	  		
	УстановитьОтбор(ФиксированнаяСтруктура);
		
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьОтбор(Отбор)

	Элементы.СписокМетаданных.ОтборСтрок = Отбор;

КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементКоллекцииОтбораМетаданных(СтрокаРодитель, Значение)

	НоваяПодчиненнаяСтрока = СтрокаРодитель.Строки.Добавить();
	НоваяПодчиненнаяСтрока.Значение = Значение;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоОтборМетаданных()
	
	ОтборМетаданныхДерево = РеквизитФормыВЗначение("ОтборМетаданных");
	
	НоваяСтрокаРодитель = ОтборМетаданныхДерево.Строки.Добавить();
	НоваяСтрокаРодитель.Значение = "Метаданные";
	
	ДобавитьЭлементКоллекцииОтбораМетаданных(НоваяСтрокаРодитель, "Справочник");
	ДобавитьЭлементКоллекцииОтбораМетаданных(НоваяСтрокаРодитель, "ПланВидовХарактеристик");
	
	ЗначениеВРеквизитФормы(ОтборМетаданныхДерево, "ОтборМетаданных");
	
КонецПроцедуры	

#КонецОбласти // СлужебныеПроцедурыИФункции


