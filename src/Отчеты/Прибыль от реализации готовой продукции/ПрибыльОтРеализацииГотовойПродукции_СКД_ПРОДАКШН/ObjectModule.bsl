
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеИсточника = ПодготовитьДанныеИсточникаДляСКД();
	
	СхемаКомпоновки	    = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных1");
	НастройкиКомпоновки = КомпоновщикНастроек.Настройки;
	КомпоновщикМакета   = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки		= КомпоновщикМакета.Выполнить(СхемаКомпоновки, НастройкиКомпоновки, ДанныеРасшифровки);
	
	ВнешнийИсточник 	= Новый Структура("ДанныеИсточника", ДанныеИсточника);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешнийИсточник, ДанныеРасшифровки);
	
	ДокументРезультат.Очистить();
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина); 
	       	
КонецПроцедуры

// Данный отчет не учитывает корректировки записей регистров, т.к. в текущем
// периоде могут быть корректировки прошлых периодов по регистрам Продажи.
// Устанавливается отбор по документам, сделавшим движения по регистру Хозрасчетный.
// 
Функция ПолучитьТекстЗапроса()
	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПродажиОбороты.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ПродажиОбороты.Номенклатура КАК Продукция,
		|	ПродажиОбороты.ХарактеристикаНоменклатуры КАК ХарактеристикаПродукции,
		|	ПродажиОбороты.КоличествоОборот КАК Количество,
		|	ПродажиОбороты.СтоимостьОборот КАК СуммаСНДС,
		|	ПродажиОбороты.НДСОборот КАК НДС,
		|	ПродажиОбороты.СтоимостьОборот - ПродажиОбороты.НДСОборот КАК СуммаБезНДС,
		|	ПродажиОбороты.ЗаказПокупателя.Контрагент КАК Контрагент,
		|	ПродажиОбороты.Регистратор КАК Регистратор,
		|	РасчетыПоРеализацииОрганизации.КоррСубконто1 КАК НомГруппа,
		|	ПродажиОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента
		|ПОМЕСТИТЬ вт_ПродажиСНомГруппой
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&НачПериода, &КонПериода, Авто, ГОД(ДокументПродажи.Дата) = ГОД(&КонПериода)) КАК ПродажиОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасчетыПоРеализацииОрганизации КАК РасчетыПоРеализацииОрганизации
		|		ПО ПродажиОбороты.Регистратор = РасчетыПоРеализацииОрганизации.Регистратор
		|			И ПродажиОбороты.Номенклатура = РасчетыПоРеализацииОрганизации.Номенклатура
		|			И ПродажиОбороты.ЗаказПокупателя = РасчетыПоРеализацииОрганизации.Сделка
		|ГДЕ
		|	ПродажиОбороты.Регистратор В
		|			(ВЫБРАТЬ
		|				ХозрасчетныйОбороты.Регистратор КАК Регистратор
		|			ИЗ
		|				РегистрБухгалтерии.Хозрасчетный.Обороты(&НачПериода, &КонПериода, Авто, Счет В (&СчетУчета_90_01_1), , , , ) КАК ХозрасчетныйОбороты
		|			СГРУППИРОВАТЬ ПО
		|				ХозрасчетныйОбороты.Регистратор)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Затрата,
		|	ВложенныйЗапрос.ХарактеристикаЗатраты,
		|	ВложенныйЗапрос.Количество,
		|	ВложенныйЗапрос.Себестоимость,
		|	ВЫБОР ВложенныйЗапрос.Количество
		|		КОГДА 0
		|			ТОГДА 0
		|		ИНАЧЕ ВложенныйЗапрос.Себестоимость / ВложенныйЗапрос.Количество
		|	КОНЕЦ КАК СебестоимостьНаЕд,
		|	ВложенныйЗапрос.НомГруппа
		|ПОМЕСТИТЬ втУчетЗатратРегл_МПЗ
		|ИЗ
		|	(ВЫБРАТЬ
		|		АУЗ.Затрата КАК Затрата,
		|		АУЗ.ХарактеристикаЗатраты КАК ХарактеристикаЗатраты,
		|		СУММА(УчетЗатратРегл.Количество) КАК Количество,
		|		СУММА(УчетЗатратРегл.Стоимость) КАК Себестоимость,
		|		АУПЗ.Субконто1 КАК НомГруппа
		|	ИЗ
		|		РегистрНакопления.УчетЗатратРегл КАК УчетЗатратРегл
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаВидаУчета КАК АВУ
		|			ПО УчетЗатратРегл.АналитикаВидаУчета = АВУ.Ссылка
		|				И (АВУ.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.МПЗ))
		|				И (АВУ.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ГотоваяПродукция))
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаЗатрат КАК АУЗ
		|			ПО УчетЗатратРегл.АналитикаУчетаЗатрат = АУЗ.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПрочихЗатрат КАК АУПЗ
		|			ПО УчетЗатратРегл.КорАналитикаВидаУчета = АУПЗ.Ссылка
		|	ГДЕ
		|		УчетЗатратРегл.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		И УчетЗатратРегл.КодОперации = ЗНАЧЕНИЕ(Перечисление.КодыОперацийПартииТоваров.Реализация)
		|		И УчетЗатратРегл.Период МЕЖДУ &НачПериода И &КонПериода
		|	
		|	СГРУППИРОВАТЬ ПО
		|		АУЗ.Затрата,
		|		АУЗ.ХарактеристикаЗатраты,
		|		АУПЗ.Субконто1) КАК ВложенныйЗапрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВложенныйЗапрос.СтоимостьЗатраты) КАК СтоимостьЗатраты,
		|	ВложенныйЗапрос.ЗаказПокупателяИспользуетсяПоГОЗ,
		|	ВложенныйЗапрос.ЗаказПокупателя,
		|	ВложенныйЗапрос.Продукция,
		|	ВложенныйЗапрос.ХарактеристикаПродукции,
		|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
		|	СУММА(ВложенныйЗапрос.СуммаСНДС) КАК СуммаСНДС,
		|	СУММА(ВложенныйЗапрос.НДС) КАК НДС,
		|	СУММА(ВложенныйЗапрос.СуммаБезНДС) КАК СуммаБезНДС,
		|	ВложенныйЗапрос.Контрагент,
		|	ВложенныйЗапрос.Регистратор,
		|	ВложенныйЗапрос.НомГруппа,
		|	ВложенныйЗапрос.ДоговорКонтрагента
		|ПОМЕСТИТЬ вт_МПЗ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЕСТЬNULL(вт_ПродажиСНомГруппой.Количество * втУчетЗатратРегл.СебестоимостьНаЕд, 0) КАК СтоимостьЗатраты,
		|		ВЫБОР
		|			КОГДА КонтрактыСЗаказчиками.Ссылка ЕСТЬ НЕ NULL 
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК ЗаказПокупателяИспользуетсяПоГОЗ,
		|		вт_ПродажиСНомГруппой.ЗаказПокупателя КАК ЗаказПокупателя,
		|		вт_ПродажиСНомГруппой.Продукция КАК Продукция,
		|		вт_ПродажиСНомГруппой.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|		вт_ПродажиСНомГруппой.Количество КАК Количество,
		|		вт_ПродажиСНомГруппой.СуммаСНДС КАК СуммаСНДС,
		|		вт_ПродажиСНомГруппой.НДС КАК НДС,
		|		вт_ПродажиСНомГруппой.СуммаБезНДС КАК СуммаБезНДС,
		|		вт_ПродажиСНомГруппой.Контрагент КАК Контрагент,
		|		вт_ПродажиСНомГруппой.Регистратор КАК Регистратор,
		|		вт_ПродажиСНомГруппой.НомГруппа КАК НомГруппа,
		|		вт_ПродажиСНомГруппой.ДоговорКонтрагента КАК ДоговорКонтрагента
		|	ИЗ
		|		вт_ПродажиСНомГруппой КАК вт_ПродажиСНомГруппой
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтрактыСЗаказчиками КАК КонтрактыСЗаказчиками
		|			ПО вт_ПродажиСНомГруппой.ДоговорКонтрагента = КонтрактыСЗаказчиками.Договор
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втУчетЗатратРегл_МПЗ КАК втУчетЗатратРегл
		|			ПО вт_ПродажиСНомГруппой.Продукция = втУчетЗатратРегл.Затрата
		|				И вт_ПродажиСНомГруппой.ХарактеристикаПродукции = втУчетЗатратРегл.ХарактеристикаЗатраты
		|				И вт_ПродажиСНомГруппой.НомГруппа = втУчетЗатратРегл.НомГруппа) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Продукция,
		|	ВложенныйЗапрос.ЗаказПокупателяИспользуетсяПоГОЗ,
		|	ВложенныйЗапрос.ХарактеристикаПродукции,
		|	ВложенныйЗапрос.Контрагент,
		|	ВложенныйЗапрос.ЗаказПокупателя,
		|	ВложенныйЗапрос.Регистратор,
		|	ВложенныйЗапрос.ДоговорКонтрагента,
		|	ВложенныйЗапрос.НомГруппа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ПродажиСНомГруппой.Регистратор,
		|	вт_ПродажиСНомГруппой.ЗаказПокупателя,
		|	вт_ПродажиСНомГруппой.СуммаСНДС,
		|	вт_ПродажиСНомГруппой.НДС,
		|	вт_ПродажиСНомГруппой.СуммаБезНДС,
		|	вт_ПродажиСНомГруппой.Контрагент,
		|	вт_ПродажиСНомГруппой.НомГруппа,
		|	ВЫБОР ЕСТЬNULL(вт_ПродажиСНомГруппой.Количество, 0)
		|		КОГДА 0
		|			ТОГДА ВложенныйЗапрос.КоличествоВыпуск * ВложенныйЗапрос.СебестНаЕд
		|		ИНАЧЕ вт_ПродажиСНомГруппой.Количество * ВложенныйЗапрос.СебестНаЕд
		|	КОНЕЦ КАК СтоимостьЗатраты,
		|	ВЫБОР ЕСТЬNULL(вт_ПродажиСНомГруппой.Количество, 0)
		|		КОГДА 0
		|			ТОГДА ВложенныйЗапрос.КоличествоВыпуск
		|		ИНАЧЕ вт_ПродажиСНомГруппой.Количество
		|	КОНЕЦ КАК Количество,
		|	ВложенныйЗапрос.Номенклатура КАК Затрата,
		|	ВложенныйЗапрос.ХарактеристикаНоменклатуры КАК ХарактеристикаЗатраты
		|ПОМЕСТИТЬ втУчетЗатратРегл_Услуги
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВложенныйЗапрос.Номенклатура КАК Номенклатура,
		|		ВложенныйЗапрос.Количество КАК КоличествоВыпуск,
		|		ВложенныйЗапрос.Стоимость КАК Стоимость,
		|		ВложенныйЗапрос.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|		ВложенныйЗапрос.НомГруппа КАК НомГруппа,
		|		ВЫБОР ЕСТЬNULL(ВложенныйЗапрос_Продажи.Количество, 0)
		|			КОГДА 0
		|				ТОГДА ВложенныйЗапрос.Стоимость / ВложенныйЗапрос.Количество
		|			ИНАЧЕ ВложенныйЗапрос.Стоимость / ВложенныйЗапрос_Продажи.Количество
		|		КОНЕЦ КАК СебестНаЕд
		|	ИЗ
		|		(ВЫБРАТЬ
		|			АРЗ.Продукция КАК Номенклатура,
		|			СУММА(УчетЗатратРегл.Количество) КАК Количество,
		|			СУММА(УчетЗатратРегл.Стоимость) КАК Стоимость,
		|			АРЗ.ХарактеристикаПродукции КАК ХарактеристикаНоменклатуры,
		|			АУПЗ.Субконто1 КАК НомГруппа
		|		ИЗ
		|			РегистрНакопления.УчетЗатратРегл КАК УчетЗатратРегл
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаВидаУчета КАК АВУ
		|				ПО УчетЗатратРегл.АналитикаВидаУчета = АВУ.Ссылка
		|					И (АВУ.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Выпуск))
		|					И (АВУ.СчетУчета = &СчетУчета_20_01_1)
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПрочихЗатрат КАК АУПЗ
		|				ПО УчетЗатратРегл.КорАналитикаВидаУчета = АУПЗ.Ссылка
		|					И (АУПЗ.СчетУчета = &СчетУчета_90_02_1)
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаРаспределенияЗатрат КАК АРЗ
		|				ПО УчетЗатратРегл.АналитикаРаспределенияЗатрат = АРЗ.Ссылка
		|		ГДЕ
		|			УчетЗатратРегл.Период МЕЖДУ &НачПериода И &КонПериода
		|		
		|		СГРУППИРОВАТЬ ПО
		|			АУПЗ.Субконто1,
		|			АРЗ.ХарактеристикаПродукции,
		|			АРЗ.Продукция) КАК ВложенныйЗапрос
		|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|				вт_ПродажиСНомГруппой.Продукция КАК Продукция,
		|				вт_ПродажиСНомГруппой.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|				СУММА(вт_ПродажиСНомГруппой.Количество) КАК Количество,
		|				вт_ПродажиСНомГруппой.НомГруппа КАК НомГруппа
		|			ИЗ
		|				вт_ПродажиСНомГруппой КАК вт_ПродажиСНомГруппой
		|			
		|			СГРУППИРОВАТЬ ПО
		|				вт_ПродажиСНомГруппой.НомГруппа,
		|				вт_ПродажиСНомГруппой.Продукция,
		|				вт_ПродажиСНомГруппой.ХарактеристикаПродукции) КАК ВложенныйЗапрос_Продажи
		|			ПО ВложенныйЗапрос.Номенклатура = ВложенныйЗапрос_Продажи.Продукция
		|				И ВложенныйЗапрос.ХарактеристикаНоменклатуры = ВложенныйЗапрос_Продажи.ХарактеристикаПродукции
		|				И ВложенныйЗапрос.НомГруппа = ВложенныйЗапрос_Продажи.НомГруппа) КАК ВложенныйЗапрос
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ПродажиСНомГруппой КАК вт_ПродажиСНомГруппой
		|		ПО ВложенныйЗапрос.Номенклатура = вт_ПродажиСНомГруппой.Продукция
		|			И ВложенныйЗапрос.ХарактеристикаНоменклатуры = вт_ПродажиСНомГруппой.ХарактеристикаПродукции
		|			И ВложенныйЗапрос.НомГруппа = вт_ПродажиСНомГруппой.НомГруппа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_МПЗ.ЗаказПокупателя,
		|	вт_МПЗ.Продукция,
		|	вт_МПЗ.ХарактеристикаПродукции,
		|	вт_МПЗ.Количество КАК Количество,
		|	вт_МПЗ.СуммаСНДС КАК СуммаСНДС,
		|	вт_МПЗ.НДС КАК НДС,
		|	вт_МПЗ.СуммаБезНДС КАК СуммаБезНДС,
		|	вт_МПЗ.СтоимостьЗатраты КАК СтоимостьЗатраты,
		|	вт_МПЗ.Контрагент,
		|	вт_МПЗ.Регистратор,
		|	вт_МПЗ.НомГруппа
		|ИЗ
		|	вт_МПЗ КАК вт_МПЗ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	вт_Услуги.ЗаказПокупателя,
		|	вт_Услуги.Затрата,
		|	вт_Услуги.ХарактеристикаЗатраты,
		|	вт_Услуги.Количество,
		|	вт_Услуги.СуммаСНДС,
		|	вт_Услуги.НДС,
		|	вт_Услуги.СуммаБезНДС,
		|	вт_Услуги.СтоимостьЗатраты,
		|	вт_Услуги.Контрагент,
		|	вт_Услуги.Регистратор,
		|	вт_Услуги.НомГруппа
		|ИЗ
		|	втУчетЗатратРегл_Услуги КАК вт_Услуги";
		
		Возврат ТекстЗапроса;
		
КонецФункции

Функция ПодготовитьДанныеИсточникаДляСКД() 
			
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапроса();
		
	Запрос.УстановитьПараметр("КонПериода",        КонецДня(КонПериода));
	Запрос.УстановитьПараметр("НачПериода",        НачПериода);
	Запрос.УстановитьПараметр("Организация",       Организация);
	Запрос.УстановитьПараметр("Счет26",            ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы);
	Запрос.УстановитьПараметр("Счет44", 		   ПланыСчетов.Хозрасчетный.РасходыНаПродажу);
	Запрос.УстановитьПараметр("Счет90", 		   ПланыСчетов.Хозрасчетный.Продажи);
	Запрос.УстановитьПараметр("СчетУчета_90_02_1", ПланыСчетов.Хозрасчетный.НайтиПоКоду("90.02.1"));
	Запрос.УстановитьПараметр("СчетУчета_20_01_1", ПланыСчетов.Хозрасчетный.НайтиПоКоду("20.01.1"));
	Запрос.УстановитьПараметр("СчетУчета_90_01_1", ПланыСчетов.Хозрасчетный.НайтиПоКоду("90.01.1"));
		
	РезультатЗапроса = Запрос.Выполнить();
	
	тзПромежуточныеВычисления = РезультатЗапроса.Выгрузить();
	ДанныеИсточника = ОбработатьТаблицуЗначений(тзПромежуточныеВычисления);
	
	Возврат ДанныеИсточника;
	 			
КонецФункции

Функция ОбработатьТаблицуЗначений(ТаблицаЗначений)
	
	// На данный момент по всем позициям себестоимость получена,
	// ее калькуляция в разрезе номенклатуры не всегда верна,
	// но итоги сходятся с ОСВ.
	// Требуется получить себестоимость по МПЗ в разрезе ном.групп,
	// более корректно. ОКР не трогаем. Получим показатели себестоимости 
	// и выручки по ном.группам.
	ТаблицаПоказателейПоНомГруппам = ПолучитьПоказателиПоНомГруппам();
	Для Каждого СтрокаПоказатель Из ТаблицаПоказателейПоНомГруппам Цикл
		Если СтрокаПоказатель.Выручка = 0 Тогда
			Сообщить("По номенклатурной группе: " + СтрокаПоказатель.НомГруппа + " выручка равна 0!");
			Продолжить;
		КонецЕсли;
		
		Итого = 0;
		
		Коэффициент = СтрокаПоказатель.Себестоимость / СтрокаПоказатель.Выручка;
		
		Отбор = Новый Структура("НомГруппа", СтрокаПоказатель.НомГруппа);
		Поиск = ТаблицаЗначений.НайтиСтроки(Отбор);
        Для Каждого СтрокаПоиска Из Поиск Цикл
			СтрокаПоиска.СтоимостьЗатраты = СтрокаПоиска.СуммаБезНДС * Коэффициент;
			Итого = Итого + СтрокаПоиска.СтоимостьЗатраты; 
		КонецЦикла;	
		
		Итого = Окр(Итого, 2);
		
		Если СтрокаПоказатель.Себестоимость <> Итого Тогда
			Сообщить("Себестоимости по ном.группе " + СтрокаПоказатель.НомГруппа + " в ОСВ равна: " + СтрокаПоказатель.Себестоимость);
			Сообщить("Калькуляция себестоимости по ном.группе " + СтрокаПоказатель.НомГруппа + " равна: " + Итого);
			Сообщить("Итоговая калькуляция по себестоимости отличается от показетелей в ОСВ по данной номенклатурной группе!
					 |Проверьте регистр продажи на корректность стоимостных показателей.");
			Сообщить("----------------------------------------");
		КонецЕсли;	
	КонецЦикла;	
	
	ТаблицаЗначений.Колонки.Добавить("Сумма26", Новый ОписаниеТипов("Число"));
	ТаблицаЗначений.Колонки.Добавить("Сумма44", Новый ОписаниеТипов("Число"));
		
	// Счет 26 - ОбщехозяйственныеРасходы	
	ТаблицаКРаспределению26 = ПолучитьСтруктуруСуммРаспределенияПо26Счету();
		
	// Счет 44 - РасходыНаПродажу
	ТаблицаКРаспределению44 = ПолучитьСуммуРаспределенияПо44Счету();
		
	// Счет 26 - ОбщехозяйственныеРасходы
	Для Каждого СтрокаРаспределения Из ТаблицаКРаспределению26 Цикл
		Отбор = Новый Структура("НомГруппа", СтрокаРаспределения.НомГруппа);
		Поиск = ТаблицаЗначений.НайтиСтроки(Отбор);
		 
		БазаДляРаспределенияПоНомГруппе = ПолучитьБазовуюСумму(Поиск);
		РаспределитьСуммуПоБазе(СтрокаРаспределения.Сумма, Поиск, БазаДляРаспределенияПоНомГруппе, "Распределение 26");
	КонецЦикла;
	
	// Счет 44 - РасходыНаПродажу
	Для Каждого СтрокаРаспределения Из ТаблицаКРаспределению44 Цикл
		Отбор = Новый Структура("НомГруппа", СтрокаРаспределения.НомГруппа);
		Поиск = ТаблицаЗначений.НайтиСтроки(Отбор);
		 
		БазаДляРаспределенияПоНомГруппе = ПолучитьБазовуюСумму(Поиск);
		РаспределитьСуммуПоБазе(СтрокаРаспределения.Сумма, Поиск, БазаДляРаспределенияПоНомГруппе, "Распределение 44");
	КонецЦикла;
	 	  		
	РезультатФункции = ПолучитьРезультатЗапросаНаБазеТЗ(ТаблицаЗначений).Выгрузить();	
		
	Возврат РезультатФункции;
	
КонецФункции 

Функция ПолучитьСтруктуруСуммРаспределенияПо26Счету()
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК НомГруппа,
		|	ХозрасчетныйОборотыДтКт.СуммаОборот КАК Сумма
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&ПериодНач, &ПериодКон, Авто, СчетДт В ИЕРАРХИИ (&Счет90_08), , СчетКт В ИЕРАРХИИ (&Счет26), , ) КАК ХозрасчетныйОборотыДтКт";
	
	Запрос.УстановитьПараметр("ПериодКон", КонецДня(КонПериода));
	Запрос.УстановитьПараметр("ПериодНач", НачПериода);
	Запрос.УстановитьПараметр("Счет26",	   ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы);
	Запрос.УстановитьПараметр("Счет90_08", ПланыСчетов.Хозрасчетный.Продажи_УправленческиеРасходы);
		
	РезультатЗапроса = Запрос.Выполнить();
	ДетальныеЗаписи  = РезультатЗапроса.Выгрузить();
	
	Возврат ДетальныеЗаписи;
	
КонецФункции	

Процедура РаспределитьСуммуПоБазе(СуммаКРаспределению, ТЗ, БазоваяСумма, Формула)
	
	БазоваяСуммаПоУмолчанию = БазоваяСумма;
	
	Для Каждого ДетальныеЗаписи Из ТЗ Цикл
		Если БазоваяСумма <= 0 Тогда
			Прервать;
		КонецЕсли;	
		
		К          = СуммаКРаспределению / БазоваяСумма;
		Стоимость_ = Окр(ДетальныеЗаписи.СтоимостьЗатраты * К, 2);
		
		Если Формула = "Распределение 26" Тогда
			ДетальныеЗаписи.Сумма26 = Стоимость_;
		ИначеЕсли Формула = "Распределение 44" Тогда
			ДетальныеЗаписи.Сумма44 = Стоимость_;
		КонецЕсли;
			
		СуммаКРаспределению = СуммаКРаспределению - Стоимость_;
		БазоваяСумма        = БазоваяСумма - ДетальныеЗаписи.СтоимостьЗатраты; 
	КонецЦикла;
		
	БазоваяСумма = БазоваяСуммаПоУмолчанию;
	
КонецПроцедуры

Функция ПолучитьСуммуРаспределенияПо44Счету()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК НомГруппа,
		|	ХозрасчетныйОборотыДтКт.СуммаОборот КАК Сумма
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&ПериодНач, &ПериодКон, Авто, СчетДт В ИЕРАРХИИ (&счет90_07), , СчетКт В ИЕРАРХИИ (&счет44), , ) КАК ХозрасчетныйОборотыДтКт";
	
	Запрос.УстановитьПараметр("ПериодКон", 	КонецДня(КонПериода));
	Запрос.УстановитьПараметр("ПериодНач", 	НачПериода);
	Запрос.УстановитьПараметр("Счет44",		ПланыСчетов.Хозрасчетный.РасходыНаПродажу);
	Запрос.УстановитьПараметр("Счет90_07", 	ПланыСчетов.Хозрасчетный.Продажи_РасходыНаПродажу);
		
	РезультатЗапроса = Запрос.Выполнить();
	ДетальныеЗаписи  = РезультатЗапроса.Выгрузить();
		
	Возврат ДетальныеЗаписи;
	
КонецФункции	

Функция ПолучитьБазовуюСумму(МассивСтрок)

	База = 0;
	
	Для Каждого Стр Из МассивСтрок Цикл
		База = База + Стр.СтоимостьЗатраты;
	КонецЦикла;	

	Возврат База;
	
КонецФункции 

Функция ПолучитьРезультатЗапросаНаБазеТЗ(ТаблицаЗначений)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВТ.Продукция,
		|	ВТ.ХарактеристикаПродукции,
		|	ВТ.ЗаказПокупателя,
		|	ВТ.Количество,
		|	ВТ.СуммаСНДС,
		|	ВТ.НДС,
		|	ВТ.СуммаБезНДС,
		|	ВТ.СтоимостьЗатраты,
		|	ВТ.Сумма26,
		|	ВТ.Сумма44,
		|	ВТ.Контрагент,
		|	ВТ.НомГруппа КАК НоменклатурнаяГруппа,
		|	ВТ.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТ_Основная
		|ИЗ
		|	&ВТ КАК ВТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Основная.Продукция КАК Продукция,
		|	ВТ_Основная.ХарактеристикаПродукции,
		|	ВТ_Основная.Контрагент,
		|	ВТ_Основная.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ВТ_Основная.Количество КАК Количество,
		|	ВТ_Основная.СуммаСНДС КАК СуммаСНДС,
		|	ВТ_Основная.НДС КАК НДС,
		|	ВТ_Основная.СуммаБезНДС КАК СуммаБезНДС,
		|	ВТ_Основная.СтоимостьЗатраты КАК СтоимостьЗатраты,
		|	ВТ_Основная.Сумма26 КАК Сумма26,
		|	ВТ_Основная.Сумма44 КАК Сумма44,
		|	ВТ_Основная.СтоимостьЗатраты + ВТ_Основная.Сумма26 + ВТ_Основная.Сумма44 КАК ВсегоЗатрат,
		|	ВТ_Основная.СтоимостьЗатраты + ВТ_Основная.Сумма26 + ВТ_Основная.Сумма44 КАК ИтогоЗатрат,
		|	ЕСТЬNULL(ВТ_Основная.СуммаБезНДС, 0) - (ВТ_Основная.СтоимостьЗатраты + ВТ_Основная.Сумма26 + ВТ_Основная.Сумма44) КАК Прибыль,
		|	ВЫБОР ВТ_Основная.СуммаБезНДС
		|		КОГДА 0
		|			ТОГДА 0
		|		ИНАЧЕ (ВТ_Основная.СуммаБезНДС - (ВТ_Основная.СтоимостьЗатраты + ВТ_Основная.Сумма26 + ВТ_Основная.Сумма44)) / ВТ_Основная.СуммаБезНДС * 100
		|	КОНЕЦ КАК Рентабельность,
		|	ВТ_Основная.НоменклатурнаяГруппа,
		|	ВТ_Основная.Регистратор
		|ИЗ
		|	ВТ_Основная КАК ВТ_Основная";
	
	Запрос.УстановитьПараметр("ВТ", ТаблицаЗначений);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;	
	
КонецФункции 

Функция ПолучитьПоказателиПоНомГруппам()
	
	СписокСчетов = Новый СписокЗначений;
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукция);
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.ОсновноеПроизводство_);
	
	Субконто = Новый СписокЗначений;
	Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы);
	
	СписокНомГрупп = Новый СписокЗначений;
	СписокНомГрупп.Добавить(Справочники.НоменклатурныеГруппы.НайтиПоНаименованию("Авиация"));
	СписокНомГрупп.Добавить(Справочники.НоменклатурныеГруппы.НайтиПоНаименованию("Морское"));
	СписокНомГрупп.Добавить(Справочники.НоменклатурныеГруппы.НайтиПоНаименованию("Общего назначения"));
	СписокНомГрупп.Добавить(Справочники.НоменклатурныеГруппы.НайтиПоНаименованию("Ремонт"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.НомГруппа КАК НомГруппа,
		|	СУММА(ВложенныйЗапрос.Себестоимость) КАК Себестоимость,
		|	СУММА(ВложенныйЗапрос.Выручка) КАК Выручка
		|ИЗ
		|	(ВЫБРАТЬ
		|		ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК НомГруппа,
		|		ХозрасчетныйОборотыДтКт.СуммаОборот КАК Себестоимость,
		|		0 КАК Выручка
		|	ИЗ
		|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&ПериодНач, &ПериодКон, Авто, СчетДт В ИЕРАРХИИ (&Счет90_02), &Субконто, СчетКт В ИЕРАРХИИ (&КорСчета), , СубконтоДт1 В (&СписокНомГрупп)) КАК ХозрасчетныйОборотыДтКт
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВложенныйЗапрос.НомГруппа,
		|		0,
		|		ВложенныйЗапрос.ВыручкаБезНДС
		|	ИЗ
		|		(ВЫБРАТЬ
		|			ВложенныйЗапрос.НомГруппа КАК НомГруппа,
		|			СУММА(ВложенныйЗапрос.Выручка - ВложенныйЗапрос.Налог) КАК ВыручкаБезНДС
		|		ИЗ
		|			(ВЫБРАТЬ
		|				ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК НомГруппа,
		|				ХозрасчетныйОборотыДтКт.СуммаОборот КАК Выручка,
		|				0 КАК Налог
		|			ИЗ
		|				РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&ПериодНач, &ПериодКон, Авто, СчетДт В ИЕРАРХИИ (&счет62), , СчетКт В ИЕРАРХИИ (&Счет90_01), &Субконто, СубконтоКт1 В (&СписокНомГрупп)) КАК ХозрасчетныйОборотыДтКт
		|			
		|			ОБЪЕДИНИТЬ ВСЕ
		|			
		|			ВЫБРАТЬ
		|				ХозрасчетныйОборотыДтКт.СубконтоДт1,
		|				0,
		|				ХозрасчетныйОборотыДтКт.СуммаОборот
		|			ИЗ
		|				РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&ПериодНач, &ПериодКон, Авто, СчетДт В ИЕРАРХИИ (&счет90_03), &Субконто, СчетКт В ИЕРАРХИИ (&счет68), , СубконтоДт1 В (&СписокНомГрупп)) КАК ХозрасчетныйОборотыДтКт) КАК ВложенныйЗапрос
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ВложенныйЗапрос.НомГруппа) КАК ВложенныйЗапрос) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.НомГруппа";
	
	Запрос.УстановитьПараметр("ПериодКон", 		КонецДня(КонПериода));
	Запрос.УстановитьПараметр("ПериодНач", 		НачПериода);
	Запрос.УстановитьПараметр("КорСчета",		СписокСчетов);
	Запрос.УстановитьПараметр("Счет90_02", 		ПланыСчетов.Хозрасчетный.СебестоимостьПродаж);
	Запрос.УстановитьПараметр("счет62",			ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками);
	Запрос.УстановитьПараметр("счет90_01", 		ПланыСчетов.Хозрасчетный.Выручка);
	Запрос.УстановитьПараметр("Субконто", 		Субконто);
	Запрос.УстановитьПараметр("СписокНомГрупп", СписокНомГрупп);
	Запрос.УстановитьПараметр("Счет90_03", 		ПланыСчетов.Хозрасчетный.Продажи_НДС);
	Запрос.УстановитьПараметр("Счет68", 		ПланыСчетов.Хозрасчетный.РасчетыПоНалогам);
		
	РезультатЗапроса = Запрос.Выполнить();
	ДетальныеЗаписи  = РезультатЗапроса.Выгрузить();
		
	Возврат ДетальныеЗаписи;
	
КонецФункции


